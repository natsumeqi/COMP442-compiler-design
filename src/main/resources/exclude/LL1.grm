START -> PROG  . 

ADDOP -> plus  . 
ADDOP -> minus  . 
ADDOP -> or  . 

APARAMS -> EXPR APARAMSTAIL  . 
APARAMS ->  . 

APARAMSTAIL -> comma EXPR APARAMSTAIL  . 
APARAMSTAIL ->  . 

ARITHEXPR -> TERM ARITHEXPRTAIL  . 

ARITHEXPRTAIL -> ADDOP TERM ARITHEXPRTAIL  . 
ARITHEXPRTAIL ->  . 

ARRAYSIZEREPT -> lsqbr INTNUM rsqbr ARRAYSIZEREPT  . 
ARRAYSIZEREPT ->  . 

ASSIGNOP -> assign  . 

ASSIGNSTATTAIL -> ASSIGNOP EXPR  . 

CLASSDECL -> class id INHERIT lcurbr CLASSDECLBODY rcurbr semi CLASSDECL  . 
CLASSDECL ->  . 

CLASSDECLBODY -> VISIBILITY MEMBERDECL CLASSDECLBODY  . 
CLASSDECLBODY ->  . 

CLASSMETHOD -> sr id  . 
CLASSMETHOD ->  . 

EXPR -> ARITHEXPR EXPRTAIL  . 

EXPRTAIL -> RELOP ARITHEXPR  . 
EXPRTAIL ->  . 

FACTOR -> FUNCORVAR  . 
FACTOR -> intnum  . 
FACTOR -> floatnum  . 
FACTOR -> stringlit  . 
FACTOR -> lpar EXPR rpar  . 
FACTOR -> not FACTOR  . 
FACTOR -> SIGN FACTOR  . 
FACTOR -> qm lsqbr EXPR colon EXPR colon EXPR rsqbr  . 

FPARAMS -> TYPE id ARRAYSIZEREPT FPARAMSTAIL  . 
FPARAMS ->  . 

FPARAMSTAIL -> comma TYPE id ARRAYSIZEREPT FPARAMSTAIL  . 
FPARAMSTAIL ->  . 

FUNCBODY -> lcurbr METHODBODYVAR STATEMENTLIST rcurbr  . 

FUNCDECL -> func id lpar FPARAMS rpar colon FUNCDECLTAIL semi  . 

FUNCDECLTAIL -> TYPE  . 
FUNCDECLTAIL -> void  . 

FUNCDEF -> FUNCTION FUNCDEF  . 
FUNCDEF ->  . 

FUNCHEAD -> func id CLASSMETHOD lpar FPARAMS rpar colon FUNCDECLTAIL  . 

FUNCORASSIGNSTAT -> id FUNCORASSIGNSTATIDNEST  . 

FUNCORASSIGNSTATIDNEST -> INDICEREP FUNCORASSIGNSTATIDNESTVARTAIL  . 
FUNCORASSIGNSTATIDNEST -> lpar APARAMS rpar FUNCORASSIGNSTATIDNESTFUNCTAIL  . 

FUNCORASSIGNSTATIDNESTFUNCTAIL -> dot id FUNCSTATTAIL  . 
FUNCORASSIGNSTATIDNESTFUNCTAIL ->  . 

FUNCORASSIGNSTATIDNESTVARTAIL -> dot id FUNCORASSIGNSTATIDNEST  . 
FUNCORASSIGNSTATIDNESTVARTAIL -> ASSIGNSTATTAIL  . 

FUNCORVAR -> id FUNCORVARIDNEST  . 

FUNCORVARIDNEST -> INDICEREP FUNCORVARIDNESTTAIL  . 
FUNCORVARIDNEST -> lpar APARAMS rpar FUNCORVARIDNESTTAIL  . 

FUNCORVARIDNESTTAIL -> dot id FUNCORVARIDNEST  . 
FUNCORVARIDNESTTAIL ->  . 

FUNCSTATTAIL -> INDICEREP dot id FUNCSTATTAIL  . 
FUNCSTATTAIL -> lpar APARAMS rpar FUNCSTATTAILIDNEST  . 

FUNCSTATTAILIDNEST -> dot id FUNCSTATTAIL  . 
FUNCSTATTAILIDNEST ->  . 

FUNCTION -> FUNCHEAD FUNCBODY  . 

INDICEREP -> lsqbr EXPR rsqbr INDICEREP  . 
INDICEREP ->  . 

INHERIT -> inherits id NESTEDID  . 
INHERIT ->  . 

INTNUM -> intnum  . 
INTNUM ->  . 

MEMBERDECL -> FUNCDECL  . 
MEMBERDECL -> VARDECL  . 

METHODBODYVAR -> var lcurbr VARDECLREP rcurbr  . 
METHODBODYVAR ->  . 

MULTOP -> mult  . 
MULTOP -> div  . 
MULTOP -> and  . 

NESTEDID -> comma id NESTEDID  . 
NESTEDID ->  . 

PROG -> CLASSDECL FUNCDEF main FUNCBODY  . 

RELOP -> eq  . 
RELOP -> neq  . 
RELOP -> lt  . 
RELOP -> gt  . 
RELOP -> leq  . 
RELOP -> geq  . 

SIGN -> plus  . 
SIGN -> minus  . 



STATBLOCK -> lcurbr STATEMENTLIST rcurbr  . 
STATBLOCK -> STATEMENT  . 
STATBLOCK ->  . 

STATEMENT -> FUNCORASSIGNSTAT semi  . 
STATEMENT -> if lpar EXPR rpar then STATBLOCK else STATBLOCK semi  . 
STATEMENT -> while lpar EXPR rpar STATBLOCK semi  . 
STATEMENT -> read lpar VARIABLE rpar semi  . 
STATEMENT -> write lpar EXPR rpar semi  . 
STATEMENT -> return lpar EXPR rpar semi  . 
STATEMENT -> break semi  . 
STATEMENT -> continue semi  . 

STATEMENTLIST -> STATEMENT STATEMENTLIST  . 
STATEMENTLIST ->  . 

TERM -> FACTOR TERMTAIL  . 

TERMTAIL -> MULTOP FACTOR TERMTAIL  . 
TERMTAIL ->  . 

TYPE -> integer  . 
TYPE -> float  . 
TYPE -> string  . 
TYPE -> id  . 

VARDECL -> TYPE id ARRAYSIZEREPT semi  . 

VARDECLREP -> VARDECL VARDECLREP  . 
VARDECLREP ->  . 

VARIABLE -> id VARIABLEIDNEST  . 

VARIABLEIDNEST -> INDICEREP VARIABLEIDNESTTAIL  . 

VARIABLEIDNESTTAIL -> dot id VARIABLEIDNEST  . 
VARIABLEIDNESTTAIL ->  . 

VISIBILITY -> public  . 
VISIBILITY -> private  . 
VISIBILITY ->  . 


